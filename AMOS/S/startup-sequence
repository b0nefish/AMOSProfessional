SetClock LOAD

FailAt 21

If NOT EXISTS "RAM:Clipboards"
    Makedir "RAM:Clipboards"
EndIf
If NOT EXISTS "RAM:T"
    Makedir "RAM:T"
EndIf
If NOT EXISTS "RAM:ENV"
    Makedir "RAM:ENV"
    Assign "ENV:" "RAM:ENV"
EndIf

Assign "T:"       "RAM:T"
Assign "CLIPS:"   "RAM:Clipboards"

Assign >NIL: AMOSPro_Examples: Sys:AMOSPro_Examples
Assign >NIL: AMOSPro_Tutorial: Sys:AMOSPro_Tutorial
Assign >NIL: AMOSPro_Accessories: Sys:AMOSPro_Accessories
Assign >NIL: AMOSPro_Productivity1: Sys:AMOSPro_Productivity1
Assign >NIL: AMOSPro_Productivity2: Sys:AMOSPro_Productivity2
Assign >NIL: AMOSPro_Extras: Sys:AMOSPro_Extras
Assign >NIL: AMOS: Sys:AMOS1.3/AMOS
Assign >NIL: AMOS_Data: Sys:AMOS1.3/AMOS_DATA
Assign >NIL: Extras: Sys:AMOS1.3/Extras
Assign >NIL: TOME4: Sys:TOME4
Assign >NIL: TOME_GOODIES: Sys:TOME_GOODIES
Assign >NIL: TOME_UPDATE: Sys:TOME_UPDATE
Assign >NIL: CRAFT_Extras: Sys:CRAFT_Extras
Assign >NIL: Easy_AMOS: Sys:Easy_AMOS
Assign >NIL: Easy_Examples: Sys:Easy_AMOS/Easy_Examples
Assign >NIL: Easy_Tutorial: Sys:Easy_AMOS/Easy_Tutorial
Assign >NIL: Giga: Sys:Gigabyte
Assign >NIL: MPlayer: Sys:MPlayer

Assign "KEYMAPS:" "DEVS:Keymaps"
Assign "LOCALE:"  "SYS:Locale"
Assign "LIBS:"    "SYS:Classes" ADD
Assign "FONTS:"   "SYS:Fonts"
Assign "HELP:"    "LOCALE:Help" DEFER
Assign "THEMES:"  "SYS:Prefs/Presets/Themes" >NIL:
Assign "IMAGES:"  "SYS:System/Images" DEFER
Assign "PRINTERS:" "DEVS:Printers"
Assign "WANDERER:" "SYS:System/Wanderer" DEFER


If EXISTS "C:SetPatch"
    C:SetPatch QUIET
EndIf

If EXISTS "SYS:Classes/USB"
    Assign "USBCLASSES:" "SYS:Classes/USB"
    AddUSBClasses >NIL:
EndIf

If EXISTS "SYS:DiskImages"
    Assign "FDSK:" "SYS:DiskImages"
EndIf

Automount >NIL:
Mount >NIL: "DEVS:DOSDrivers/~((.#?)|(#?.info)|(#?.dbg))"

Dir >NIL: "PIPE:"

Path "C:" "SYS:System" "S:" "SYS:Prefs" "SYS:Tools" "SYS:Utilities" QUIET

If EXISTS "SYS:Tools/Commodities"
    Path "SYS:Tools/Commodities" QUIET ADD
EndIf

If EXISTS "SYS:Demos"
    Path "SYS:Demos" ADD QUIET
EndIf

If EXISTS "Sys:RexxC"
    Path "Sys:RexxC" ADD QUIET
Endif

Copy >NIL: "ENVARC:" "ENV:" ALL NOPRO NOREQ PAT "~(def_#?.info)"

If EXISTS "ENV:SYS/theme.var"
    Assign "THEME:" "${SYS/theme.var}"
else
    Assign "THEME:" "THEMES:AROSDefault"
endif

Touch >NIL: "FONTS:__TEST__"
If WARN
    Makedir "RAM:Fonts"
    Assign "FONTS:" "RAM:Fonts"
    Copy >NIL: "SYS:Fonts" "FONTS:" ALL CLONE NOREQ
Endif
Assign >NIL: "FONTS:" "FONTS:Russian" ADD

IF EXISTS "FONTS:__TEST__"
    Delete "FONTS:__TEST__" QUIET
Endif

If NOT EXISTS "FONTS:fixed.font"
    FixFonts
EndIf

AddDatatypes REFRESH QUIET
IPrefs
PsdStackLoader >NIL:
Run <NIL: >NIL: QUIET ConClip

If EXISTS "C:RexxMast"
    Assign "REXX:" "S:"
    Run <NIL: >NIL: QUIET C:RexxMast
EndIf

If EXISTS "C:Decoration"
    If EXISTS "ENV:SYS/theme.var"
        C:Decoration <NIL: >NIL:
        If EXISTS "THEME:global.prefs"
            If EXISTS "ENV:Zune/usethemeprefs"
                Copy >NIL: "THEME:global.prefs" "ENV:Zune/" NOPRO NOREQ FORCEOVERWRITE
            EndIf
        EndIf
    EndIf
Endif

Assign EXISTS "Extras:" >NIL:
If WARN
    If EXISTS "$EXTRASPATH"
        Assign "Extras:" "$EXTRASPATH"
    Else
        Assign "Extras:" "SYS:Extras" >NIL:
        If WARN
            Assign "Extras:" "NIL:" >NIL:
        EndIf
    EndIf
Endif

If EXISTS "ENV:SYS/Packages"
    List "ENV:SYS/Packages" NOHEAD FILES TO "T:P" LFORMAT="If EXISTS ${SYS/Packages/%N}*NCD ${SYS/Packages/%N}*NIf EXISTS S/%N-Startup*NExecute S/%N-Startup*NElse*NIf EXISTS S/Package-Startup*NExecute S/Package-Startup*NEndif*NEndif*NEndif*N"
    Execute "T:P"
    Delete "T:P" QUIET
    CD "SYS:"
EndIf

Protect "S:PCD" "s" ADD QUIET
Protect "S:SPat" "s" ADD QUIET
Protect "S:DPat" "s" ADD QUIET

If EXISTS "S:User-Startup"
    Execute "S:User-Startup"
EndIf

If EXISTS "WANDERER:Wanderer"
    WANDERER:Wanderer
    
    If NOT ERROR
        EndCLI
    EndIf
EndIf
