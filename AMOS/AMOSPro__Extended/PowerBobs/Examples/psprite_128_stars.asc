'This starfield demo uses only 2 "Sprite Channels" to display 128 "Stars". 
'Only channels 0 and 1 are used. 
'  
'On a standard A1200 less than 1/2 Vbl is used! (non compiled Amos code) 
'
Screen Open 0,320,200,4,L
Wait Vbl 
Wait Vbl 
Curs Off : Flash Off 
Cls 0
Plot 0,0
Get Sprite 1,0,0 To 16,1
Wait Vbl 
Cls 0
Colour 18,$FFF
Set Psprite Colours 4
Psprite Max 128
Convert Sprites 1
Dim X(127),Y(127),IMAGE(127),STARS_SPEED(127)
For I=0 To 127
   X(I)=Rnd(320)+128
   Y(I)=I*1.5+44
   IMAGE(I)=1
   STARS_SPEED(I)=Rnd(8)+1
Next 
R=Execall(-132)
Repeat 
   Psum Varptr(X(0)),Varptr(STARS_SPEED(0)),0 To 127
   Psprite Varptr(X(0)),Varptr(Y(0)),Varptr(IMAGE(0)),0 To 127
   Wait Vbl 
   Psprite Update 
Until Left Click
R=Execall(-138)
