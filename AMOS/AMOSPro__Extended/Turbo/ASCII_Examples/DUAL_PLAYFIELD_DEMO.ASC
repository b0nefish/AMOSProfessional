'The variables _PROC_TYPE and _PROC_TYPE_OVERRIDE provide a simple control,
'allowing you to chose wether the top scrolling line is to be displayed
'or not. 
'Handy when the prog is running on a <68020 processor. 
'If _PROC_TYPE_OVERRIDE=True, the top scroller is always displayed.
'The prog might be fast enough (when compiled) to display both scrollers 
'smoothly.  Never tested it! It runs very smooth on my A1200, even if it's 
'not compiled!!! (cache enabled) 
'
Global _PROC_TYPE
Global _PROC_TYPE_OVERRIDE
_PROC_TYPE=Cpu Info
_PROC_TYPE_OVERRIDE=False
Hide On 
INIT
MAIN_DEMO
Procedure INIT
   Load ":TURBO_BANKS/DUAL_PLAYFIELD_DEMO.ABK"
   Unpack 14 To 0
   SXSC=320 : SYSC=101
   Screen Open 1,SXSC,SYSC,4,Lowres
   Screen Display 0,138,125,,
   Flash Off 
   Get Palette 0
   Cls 0
   Wait Vbl 
   Screen Open 2,SXSC,SYSC,4,Lowres
   Screen Display 1,138,125,,
   Flash Off 
   Wait Vbl 
   PRI1=2 : PRI2=1
   Dual Playfield 1,2
   Dual Priority PRI1,PRI2
   Screen 1
   For I=8 To 12
      Colour I,Colour(I-8) : Rem zelfde kleur voor beide schermen 
   Next 
   Screen 2
   Cls 0
   Screen Copy 0,115,0,215,60 To 1,125,30
   Wait 50
   Screen Close 0
   BLITTER
End Proc
Procedure MAIN_DEMO
   Screen Open 3,352,33,4,Lowres
   Screen Display 3,128,214,320,
   Flash Off 
   Get Sprite Palette 
   Colour 1,Colour(1)-$666
   Blit Clear -1
   Set Rainbow 0,0,32,"(1,1,15)(1,-1,15)","(1,1,15)(1,-1,15)","(1,2,15)(1,-2,15)"
   Rainbow 0,0,224,32
   If(_PROC_TYPE>=20) or(_PROC_TYPE_OVERRIDE=True)
      Screen Clone 5
      Get Palette 3
      Screen Display 5,128,120,,
   End If 
   Blit Clear -1
   SCRL$="ABCDEFGHIJKLMNOPQRSTUVWXYZ"
   SCHUIF=0
   NEW_CHAR=32
   LIJNEN=2
   LINKS=4
   X=320
   PRI1=2 : PRI2=1
   Blit Store Left 3,19,0,0 To 352,32,-LINKS
   Set Rainbow 1,0,32,"(1,1,15)(1,-1,15)","","(1,2,15)(1,-2,15)"
   Rainbow 1,0,111,32
   A$="   Start: Let A=32;"
   A$=A$+"       Let Y=215;"
   A$=A$+"       Let X=0;"
   A$=A$+"Loop: Let X=X+1; Let X=X & 31"
   A$=A$+"      P;Jump Loop;"
   Channel 0 To Rainbow 0
   Amal 0,A$
   A$="   Start: Let A=32;"
   A$=A$+"       Let Y=121;"
   A$=A$+"       Let X=32;"
   A$=A$+"Loop: Let X=X-1; Let X=X & 31"
   A$=A$+"      P;Jump Loop;"
   Channel 0 To Rainbow 0
   Channel 1 To Rainbow 1
   Amal 1,A$
   Amal On 
   Multi No 
   Do 
      Restore 
      For I=1 To LIJNEN
         Read TEKST$
         L=Len(TEKST$)
         For CHAR=1 To L
            If Left Click
               Blit Erase -1
               Erase 1
               Erase 2
               Erase 14
               Multi Yes 
               Default 
               End 
            End If 
            E$=Mid$(TEKST$,CHAR,1)
            E=-64+Asc(E$) : E=E-82*(E<1)
            F 32 Icon X,0,E
            SCHUIF=0
            Repeat 
               If COUNT=270
                  Swap PRI1,PRI2
                  Dual Priority PRI1,PRI2
                  COUNT=0
               End If 
               Wait Vbl 
               Multi Blit 1 To 16
               Screen Copy 1,90,89,245,90 To 2,90,90
               Multi Blit 17 To 19
               Screen Copy 2,90,29,248,30 To 1,90,30
               Add SCHUIF,LINKS
               Inc COUNT
            Until SCHUIF>=NEW_CHAR
         Next 
      Next 
   Loop 
   Data "DUAL PLAYFIELD DEMO ONLY MADE POSSIBLE WITH THE USE OF "
   Data "THE AMOS AND AMOSPRO TURBO PLUS EXTENSION    "
End Proc
Procedure BLITTER
   Blit Store Left 1,1,96,30 To 256,34,-1
   Blit Store Left 1,2,96,34 To 256,38,-1
   Blit Store Left 1,3,96,38 To 256,42,-1
   Blit Store Left 1,4,96,42 To 256,46,-1
   Blit Store Left 1,5,96,74 To 256,78,1
   Blit Store Left 1,6,96,78 To 256,82,1
   Blit Store Left 1,7,96,82 To 256,86,1
   Blit Store Left 1,8,96,86 To 256,90,1
   Blit Store Left 2,9,96,30 To 256,34,-1
   Blit Store Left 2,10,96,34 To 256,38,-1
   Blit Store Left 2,11,96,38 To 256,42,-1
   Blit Store Left 2,12,96,42 To 256,46,-1
   Blit Store Left 2,13,96,74 To 256,78,1
   Blit Store Left 2,14,96,78 To 256,82,1
   Blit Store Left 2,15,96,82 To 256,86,1
   Blit Store Left 2,16,96,86 To 256,90,1
   Blit Store Up 1,17,90,30 To 256,90,1
'   Screen Copy 1,90,30,48,90 To 1,90,31 
   Blit Store Up 2,18,90,30 To 256,91,-1
'   Screen Copy 2,90,30,248,91 To 2,90,29
End Proc
